buildscript {
    ext {
        javaVersion = JavaVersion.VERSION_1_8
        gradleVersion = "6.6.1"
        kotlinVersion = "1.4.10"
        ktlintGradleVersion = "9.4.0"
        ktlintVersion = "0.39.0"
        detektVersion = "1.13.1"
        hivemqMqttClientVersion = "1.2.1"
        kotlinCoroutineVersion = "1.3.9"
        picoVersion = "4.5.2"
    }

    repositories {
        gradlePluginPortal()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        classpath "gradle.plugin.com.palantir.graal:gradle-graal:0.7.1-20-g113a84d"

        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlintGradleVersion"
    }
}

repositories {
    jcenter()
}

apply plugin: "kotlin"
apply plugin: "kotlin-kapt"
apply plugin: "application"
apply plugin: 'com.palantir.graal'
apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: "org.jlleitschuh.gradle.ktlint"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    implementation "info.picocli:picocli:$picoVersion"
    kapt "info.picocli:picocli-codegen:$picoVersion"

    implementation "com.hivemq:hivemq-mqtt-client:$hivemqMqttClientVersion"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$kotlinCoroutineVersion"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict", "-Xjvm-default=enable", "-Xnew-inference"]
        jvmTarget = javaVersion.toString()
        allWarningsAsErrors = true
    }
}

kapt {
    arguments {
        arg("project", "${project.name}")
    }
}

detekt {
    config = files("${project.rootDir}/detekt.yml")

    buildUponDefaultConfig = true

    reports {
        xml.destination = file("$buildDir/reports/detekt/detekt.xml")
        html.destination = file("$buildDir/reports/detekt/detekt.html")
    }
}

nativeImage.dependsOn check
nativeImage.dependsOn jar

graal {
    mainClass 'de.smartsquare.kortance.KortanceKt'
    outputName 'kortance'
    option "--no-fallback"
    option "--no-server"
    option "--report-unsupported-elements-at-runtime"
}

application {
    mainClassName = "de.smartsquare.kortance.KortanceKt"
}
